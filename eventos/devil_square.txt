// .----------------------------------------------------------.
// .            SoulBlaker Scripts Apresenta:                 .
// .                ___________________                       .
// .               /   _____/\______   \                      .
// .               \_____  \  |    |  _/                      .
// .               /        \ |    |   \                      .
// .              /_______  / |______  /                      .
// .                      \/         \/                       .
// .            [ Cronus Advanced Scripts ]                   .
// .                                                    v1.1  .
// .----------------------------------------------------------.
// ;  http://forum.cronus-emulator.com/user/20800-soulblaker  ;
// .----------------------------------------------------------.
// . Script:                                                  .
// .  Devil Square                                            .
// .----------------------------------------------------------.
// . Por: SoulBlaker   Compátivel: Cronus       versão: 1.8   .
// .----------------------------------------------------------.
// . Descrição:                                               .
// .  Evento Devil Square, ocorre de 1h em 1h.                .
// .----------------------------------------------------------.
// . Changelog da Versão:                                     .
// .  * 1.7 [SoulBlaker]                                      .
// .	- Trazido do eAthena na versão 1.7.                   .
// .                                                          .
// .  * 1.8 [SoulBlaker]                                      .
// .    - Removido mapflag nogo que não há suporte mais.      .
// *----------------------------------------------------------*
// ;                  Changelog eAthena                       ;
// .----------------------------------------------------------.
// . Changelog da Versão:                                     .
// .   - 1.0 Iniciado o NPC. [SoulBlaker]                     .
// .   - 1.1 Corrigido nome de uma var. [SoulBlaker]	      .
// .   - 1.2 Corrigido evento da Square 2. [SoulBlaker]       .
// .   - 1.3 Corrigido info para ver se a Devil Square já     .
// .	     está acontecendo. [SoulBlaker]                   .
// .   - 1.4 Fixado erro quando todos jogadores morrem na     .
// .         Devil Square e adicionado todos anuncios em      .
// .         verde e mapflag nowarpto, também foi adicionado  .
// .         a checagem e remoção de Zeny e a BaseLevel da    .
// .         Devil Square. [SoulBlaker]                       .
// .  - 1.5 Reforçado a checagem de players que restão na     .
// .        arena com uma váriavel Global. [SoulBlaker]       .
// .  - 1.6 Adicionado Sleep. [SoulBlaker]                    .
// .        Tempo para Eliminação do Evento da DS.            .
// .  - 1.7 Adicionado Suporte a um Monstro Assassino.        .
// .        A pedidos de alguns Membros. [SoulBlaker]         .
// *----------------------------------------------------------*
prontera,156,300,5	script	Devil Square#devil_01	599,{

	// Configurações

	// Qual nível de Base para Entrar na Devil Square?
	set $SquareBaseLevel, 1;

	// Qual o custo de Zeny para entrar na Devil Square?
	set $SquareZenyCost, 0;

	// Configurações das Recompensas
	// Os itens são entregue aos jogadores que quebrarem
	// o Arca do Tesouro ao terminar o combate contra os
	// monstros. Logo abaixo, terá uma série de informações
	// e configurações, para você configurar da melhor
	// maneira que achar para o seu servidor.

	// Configurações de EXP
	// Quanto de Experiência de Base o Jogador vai ganhar
	// ao matar uma Arca do Tesouro?
	// Se você deixar com o valor '0', o jogador não vai
	// ganhar experiência de Base.
	set $SquareBaseEXP,0;

	// Quanto de Experiência de Classe o jogador vai ganhar
	// ao matar uma Arca do Tesouro?
	// Se você deixar com o valor '0', o jogador não vai
	// ganhar experiência de Classe.
	set $SquareJobExp,0;

	// Configurações dos Itens!
	// Os itens são gravados e váriaveis (arrays).
	// Você deve determinar o ponto da array e o valor dela
	// como o Exemplo abaixo:
	//
	// set $SquareItem[<ponto>],<id do item>;
	// set $SquareItem[0],501;
	// OBS: Aqui o valor do array $SquareItem[0] será 501.
	//
	// set $SquareItem[0],501;
	// set $SquareItem[1],502;
	// OBS: Aqui o valor do ponto '0' da Array $SquareItem
	//      será 501, e o ponto '1' será 502.
	//
	// Você somente pode adicionar números no ponto,
	// e não utrapassar de 127 arrays. Ou seja o máximo de
	// ponto no é '127'.
	//
	// Se você for um usuário avançado, poderá adicionar vários
	// valores em um setarray.
	// Exemplo:
	//
	// setarray $SquareItem[0],501,502,503;
	//
	// Ele irá setar desta maneira: 0 = 501, 1 = 502 e 3 = 503.
	// Alguns Itens vem padarão para não haver erro durante os eventos.
	// Então Sinta-se livre para setar os itens que deseja ou editar
	// os padrão do script. (IMG:http://www.eathena.ws/board/style_emoticons/default/smile.gif) 
	set $SquareItem[0],501;
	set $SquareItem[1],502;
	set $SquareItem[2],503;

	// Qual o Máximo da quantidade de Item que o jogador pode ganhar?
	// Se você defenir o valor para 10, o jogador irá ganhar 10x o
	// item ganho no evento.
	set $SquareAmount,1;

	// Qual o Máximo de Jogador que pode entrar na arena de Devil Square?
	// Se você botar '0', está opção será desabilitada.
	set $SquarePlayers,10;

	// Deseja bloquear o uso de Habilidades ao gerar a Arca do Tesouro?
	// Bote 1 se deseja, bote 0 se não deseja.
	set $SquareTreasureNoSkill,0;

	// Quanto tempo de checagem se não tem mais jogadores no Evento (Todos Foram Eliminados).
	// Em Milisegundos
	// 15000 = 15segundos
	set $Square_eliminate_time, 15000;

	// Habilitar Monstro Mode Assassino?
	// Ponha '0' se deseja desabilitar.
	set $Square_monster_assasin,1;

	mes "[Recepcionista da Devil Square]";
	mes "Seja Bem-vindo,";
	mes "a ^ff0000 Devil Square ^000000.";
	mes "Se você façar você morre, a";
	mes "porta é fechada e você deve";
	mes "esperar na sala de Espera da Devil Square!!";
	next;
	switch ( select ("Sala de Espera?","Informações.","Saír.") ) {
		case 1:
			mes "[Recepcionista da Devil Square]";
			mes " Sim, para entrar na Devil Square...";
			mes "... você deve clicar duas vezes";
			mes "no balão escrito 'Devil Square' emcima de mim.";
			mes " Assim que estiver lá, aguarde o inicio da";
			mes "Devil Square que você será teletransportado";
			mes "automáticamente para a arena.";
			break;
		case 2:
			mes "[Recepcionista da Devil Square]";
			mes " A Devil Square é determinada por";
			mes "nível de Base...";
			mes " Para participar deste evento você";
			mes "deve ter o Nível de Base " + $SquareBaseLevel + " ou maior.";
			next;
			mes "[Recepcionista da Devil Square]";
			if( $SquareZenyCost ) {
				mes " A para entrar na arena";
				mes "você precisa ter no minimo de";
				mes $SquareZenyCost + " zenys.";
				mes " Mais pode trazer muitos";
				mes "beneficios a você.";
			}
			else {
				mes " Para entrar na arena";
				mes "é totalmente grátis.";
				mes "Ou seja não leva nenhum";
				mes "custo, e pode trazer";
				mes "muitos beneficios...";
			}
			next;
			mes "[Recepcionista da Devil Square]";
			mes "No final do evento, será invocado";
			mes "algumas Arca do Tesouro, possibilitando";
			mes "você a mata-las e ganhar recompensas";
			mes "do evento, preste muita atenção quando";
			mes "elas forem invocadas...";
			next;
			mes "[Recepcionista da Devil Square]";
			mes " A Devil Square ocorre de 1 em 1 hora.";
			mes "Então fique sempre ligado nestes horários.";
			break;
		case 3:
			mes "[Recepcionista da Devil Square]";
			mes "Já sabe, se falar morre...";
			break;
	}
	close;

OnInit:
	// Sets mapflags
	setmapflag "ordeal_1-1",mf_nomemo;
	setmapflag "ordeal_1-1",mf_noteleport;
	setmapflag "ordeal_1-1",mf_nowarp;
	setmapflag "ordeal_1-1",mf_nowarpto;
	setmapflag "ordeal_1-1",mf_nomemo;

	// Set Waiting Rom
	set $@player,10000;
	if( $SquarePlayers )
		set $@player, $SquarePlayers;

	waitingroom "Devil Square",$@player;
	disablenpc "Devil Square#check";
	end;

OnMinute00:
	killmonsterall "ordeal_1-1";
	if( $squareinf == 0 ) {
		set $@clock,5;
		donpcevent "Devil Square#devil_01::OnSquareEvent";
	}
	end;
OnMinute01:
	if( $squareinf == 0 ) {
		set $@clock,4;
		donpcevent "Devil Square#devil_01::OnSquareEvent";
	}
	end;
OnMinute02:
	if( $squareinf == 0) {
		set $@clock,3;
		donpcevent "Devil Square#devil_01::OnSquareEvent";
	}
	end;
OnMinute03:
	if( $squareinf == 0 ) {
		set $@clock,2;
		donpcevent "Devil Square#devil_01::OnSquareEvent";
	}
	end;
OnMinute04:
	if( $squareinf == 0 ) {
		set $@clock,1;
		donpcevent "Devil Square#devil_01::OnSquareEvent";
	}
	end;
OnMinute05:
	if( $squareinf == 0 ) {
		set $@clock,0;
		disablenpc "Devil Square#exit";
		donpcevent "Devil Square#devil_01::OnSquareEvent";
	}
	end;

OnSquareEvent:
	// Tome cuidado ao editar os eventos.
	if( $@clock > 0 )
		announce "A Devil Square está-ra aberta em " + $@clock + " minutos, corram para a arena!",bc_all,"0x00FF00";
	else if( $square_round == 0 || $square_round == 1) {
		announce "A Devil Square começou, boa sorte para todos!",bc_all,"0x00FF00";
		
		enablenpc "Devil Square#check";
		warpwaitingpc "ordeal_1-1",183,182,10000;

		set $squareinf, 1;
		set $squaremob, 74;
		set $square_round, 1;

		// Mobs Padrão de 15 Spaws
		areamonster "ordeal_1-1",183,182,246,144,"[Devil Square] Orc Herói",1189,15,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,144,"[Devil Square] Esqueleto Arqueiro",1016,15,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,144,"[Devil Square] Minorous",1149,15,"Devil Square#devil_01::OnDevilMobDead";
		
		// Mobs Padrão de 10 Spaws
		areamonster "ordeal_1-1",183,182,246,144,"[Devil Square] Esqueleto Soldado",1028,10,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,144,"[Devil Square] Arenoso",1165,10,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,144,"[Devil Square] Réquiem",1164,10,"Devil Square#devil_01::OnDevilMobDead";

	}
	else if( $square_round == 2 ) {
		announce "Devil Square: Começou a 2° Square!! Boa Sorte a todos",bc_all,"0x00FF00";
		set $squaremob, 59;
		
		// Mobs Padrão de 15 Spaws
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Gárgula",1253,15,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Mímico",1191,15,"Devil Square#devil_01::OnDevilMobDead";
		
		// Mobs Padrão de 10 Spaws
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Alarme",1193,10,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Injustiçado",1257,10,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Alma Penada",1192,10,"Devil Square#devil_01::OnDevilMobDead";

	}
	else if( $square_round == 3 ) {
		announce "Devil Square: Começou a 3° Square!! Boa Sorte a todos",bc_all,"0x00FF00";
		set $squaremob, 58;

		// Mobs Padrão de 15 Spaws
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Anolian",1206,15,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Andarilho",1208,15,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Coringa",1131,15,"Devil Square#devil_01::OnDevilMobDead";

		// Mobs Padrão de 10 Spaws
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Deviruchi",1109,10,"Devil Square#devil_01::OnDevilMobDead";

		// Mobs Padrão de 1 Spaws
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Ilusão das Trevas",1302,1,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Senhor dos Orcs",1190,1,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Senhor das Trevas",1272,1,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Amon-ra",1511,1,"Devil Square#devil_01::OnDevilMobDead";

	}
	else if( $square_round == 4 ) {
		announce "Devil Square: Começou a 4° Square e os Mini-Boss Atacam! Boa Sorte a todos",bc_all,"0x00FF00";
		set $squaremob, 34;

		// Mobs Padrão de 6 Spaws
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Serial Killer",1507,6,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Dragão Mutante",1449,6,"Devil Square#devil_01::OnDevilMobDead";

		// Mobs Padrão de 5 Spaws
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Cavaleiro Sanguinário",1268,5,"Devil Square#devil_01::OnDevilMobDead";

		// Mobs Padrão de 4 Spaws
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Quimera",1456,4,"Devil Square#devil_01::OnDevilMobDead";

		// Mobs Padrão de 3 Spaws
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] ArchAngeling",1388,3,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Ghostring",1120,3,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Grifo",1259,3,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Panzer Goblin",1308,3,"Devil Square#devil_01::OnDevilMobDead";

		// Mobs Padrão de 2 Spaws
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Líder Cobold",1296,2,"Devil Square#devil_01::OnDevilMobDead";

	}
	else if( $square_round == 5 ) {
		announce "Devil Square: Começou a 5° Square e os MVP's Atacam! Boa Sorte a todos",bc_all,"0x00FF00";
		set $squaremob, 14;

		// Mobs Padrão de 2 Spaws
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Bafomé",1399,2,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Senhor dos Mortos",1373,2,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] General Tartaruga",1312,2,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Senhor dos Orcs",1190,2,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Senhor das Trevas",1272,2,"Devil Square#devil_01::OnDevilMobDead";

		// Mobs Padrão de 1 Spaws
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Drácula",1389,1,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Faraó",1157,1,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Osíris",1038,1,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Serpente Suprema",1418,1,"Devil Square#devil_01::OnDevilMobDead";
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Orc Herói",1087,1,"Devil Square#devil_01::OnDevilMobDead";

		// Mob Assassino
		if( $Square_monster_assasin )
			areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Assassino",1785,1,"Devil Square#devil_01::OnDevilAssDead";

	}
	else if( $square_round == 6 ) {
		set $squaremob, 9;

		// Mobs Padrão de 5 Spaws
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Amon-ra",1511,5,"Devil Square#devil_01::OnDevilMobFinish";

		// Mobs Padrão de 3 Spaws
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Osíris",1038,3,"Devil Square#devil_01::OnDevilMobFinish";

		// Mobs Padrão de 2 Spaws
		areamonster "ordeal_1-1",183,182,246,244,"[Devil Square] Senhor dos Mortos",1373,2,"Devil Square#devil_01::OnDevilMobFinish";

	}
	else {
		// Atenção, aqui fica todas as Caixas de recompensa...
		set $squaremob, 9;
		monster "ordeal_1-1",231,250,"Arca do Tesouro",1324,1,"Devil Square#devil_01::OnTreasureDied";
		monster "ordeal_1-1",234,247,"Arca do Tesouro",1328,1,"Devil Square#devil_01::OnTreasureDied";
		monster "ordeal_1-1",237,244,"Arca do Tesouro",1332,1,"Devil Square#devil_01::OnTreasureDied";
		monster "ordeal_1-1",240,241,"Arca do Tesouro",1336,1,"Devil Square#devil_01::OnTreasureDied";
		monster "ordeal_1-1",243,238,"Arca do Tesouro",1340,1,"Devil Square#devil_01::OnTreasureDied";
		monster "ordeal_1-1",246,235,"Arca do Tesouro",1344,1,"Devil Square#devil_01::OnTreasureDied";
		monster "ordeal_1-1",249,232,"Arca do Tesouro",1348,1,"Devil Square#devil_01::OnTreasureDied";
		monster "ordeal_1-1",252,229,"Arca do Tesouro",1352,1,"Devil Square#devil_01::OnTreasureDied";
		monster "ordeal_1-1",249,241,"Arca do Tesouro",1356,1,"Devil Square#devil_01::OnTreasureDied";
		monster "ordeal_1-1",240,249,"Arca do Tesouro",1360,1,"Devil Square#devil_01::OnTreasureDied";
		if( !$SquareTreasureNoSkill )
			setmapflag "ordeal_1-1",mf_noskill;

	}
	end;

OnDevilMobDead:
	set $squaremob, $squaremob - 1;
	if( $squaremob > 0 )
		announce "Devil Square: Faltam " + $squaremob + " monstros para a " + ($square_round + 1) + "° Square.",bc_all,"0x00FF00";
	else {
		set $square_round, $square_round + 1;
		donpcevent "Devil Square#devil_01::OnSquareEvent";
	}
	end;

OnDevilMobFinish:
	set $squaremob, $squaremob - 1;
	if( $squaremob > 0 )
		announce "Devil Square: Faltam " + $squaremob + " monstros para terminar os ataques dos Monstros.",bc_all,"0x00FF00";
	else {
		announce "Devil Square: Parabéns jovens guerreiros, vocês derrotaram todos ataques dos monstros. Agora as Arca do Tesouro irão aparecer.",bc_all,"0x00FF00";
		set $square_round, 7;
		donpcevent "Devil Square#devil_01::OnSquareEvent";
	}
	end;

OnTreasureDied:
	set $squaremob, $squaremob - 1;
	announce "Devil Square: O Jogador [ " + strcharinfo(0) + " ] abriu uma das Arcas do Tesouro.",bc_all,"0x00FF00";
	doevent "Devil Square#devil_01::OnTreasure";
	if( $squaremob == 0 ) {
		announce "Parabéns a todos bravos guerreiros que venceram a luta contra os Monstros na Devil Square.",bc_all,"0x00FF00";
		announce "Evento criado por [ SoulBlaker ] do fórum eAthena.",bc_all,"0x00FF00";
		enablenpc "Devil Square#exit";
		enablewaitingroomevent "Devil Square#exit";
		set $square_round, 0;
		set $squareinf, 0;

		// Remove mapflags
		removemapflag "ordeal_1-1",mf_nomemo;
		removemapflag "ordeal_1-1",mf_noteleport;
		removemapflag "ordeal_1-1",mf_nowarp;
		removemapflag "ordeal_1-1",mf_nowarpto;
		removemapflag "ordeal_1-1",mf_nomemo;
		removemapflag "ordeal_1-1",mf_noskill;

	}

	end;

OnTreasure:

	set BaseExp, BaseExp + $SquareBaseEXP;
	set JobExp, JobExp + $SquareBaseEXP;
	set $@rand_1,rand(getarraysize($SquareItem));
	if( $SquareAmount )
		set $@rand_2,rand(1,$SquareAmount);
	else
		set $@rand_2,1;
	getitem $SquareItem[$@rand_1],$@rand_2;
	end;

OnDevilAssDead:
	announce "Devil Square: O Jogador [ " + strcharinfo(0) + " ] matou o Monstro Assassino.",bc_all,"0x00FF00";
	end;

OnPCDieEvent:

	// Adicionado informações do Mapa do Jogador
	getmapxy($@map$,$@x,$@y,0);
	if( $@map$ == "ordeal_1-1" ) {
		set $square_players_event, $square_players_event - 1;
		warp "prontera",0,0;
	}

	// Tempo de Espera da Saída da Arena
	sleep $Square_eliminate_time;

	if( $squareinf && ( getmapusers("ordeal_1-1") ) == 0 )
	{
		announce "Devil Square: Todos falharam na Devil Square, os Monstros sairam vitoriosos na " + $square_round + "º Square.",bc_all,"0x00FF00";
		killmonsterall "ordeal_1-1";
		set $square_round, 0;
		set $squareinf, 0;
	}
	end;

}

ordeal_1-1,246,245,7	script	Devil Square#exit	51,{

	mes "[Recepcionista da Saída]";
	mes "Deseja retornar a Prontera?";
	next;
	if( Select ("Sim.","Não.") == 1) {
		mes "[Recepcionista da Saída]";
		mes "Muito bem, boa viajem!";
		close2;
		warp "prontera",155,171;
		end;
	}
	mes "[Recepcionista da Saída]";
	mes "Você deve saír logo... ou pode ter grandes problemas...";
	close;

OnInit:
	waitingroom "Devil Square Saída",10000;
	disablewaitingroomevent;
	end;
		
}

ordeal_1-1,183,182,4	script	Devil Square#check	111,2,2,{

OnTouch:
	if( BaseLevel < $SquareBaseLevel ) {
		dispbottom "Você não tem nível de Base para entrar na arena.";
		dispbottom "A Arena é limitada para Jogadores com o Nível de Base " + $SquareBaseLevel + " ou superior.";
		warp "prontera",0,0;
	}
	if( $SquareZenyCost && Zeny < $SquareZenyCost ) {
		dispbottom "Você não tem zeny para entrar na arena.";
		dispbottom "E será chutado para bem longe...";
		warp "prontera",0,0;
	}
	set Zeny, Zeny - $SquareZenyCost;
	end;

}
